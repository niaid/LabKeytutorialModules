// This is a sample gradle file for creating a stand-alone Java module that can be deployed in a LabKey server
// instance.  This file applies some of the LabKey Gradle plugins and thus assumes the module layout described 
// in the LabKey documentation
// (https://labkey.org/Documentation/wiki-page.view?name=moduleDirectoryStructures)
// For simplicity, the source code does not include JSP files that would be compiled and collected into a JSP jar file
// or XSD files that would be transformed into XMLBeans objects and include in the main jar.
//
// You can refer to the gradlePlugins source code (https://github.com/LabKey/gradlePlugin) for more details on the
// full build process implemented for modules within the LabKey source tree.  
//
// Then run the command
//     ./gradlew module
// This will create a .module file for your project and deposit it in the build directory
// for your standalone module.
//
// See the Gradle documentation for more information (https://docs.gradle.org).
//
import org.labkey.gradle.util.BuildUtils

buildscript {
    repositories {
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        if (gradlePluginsVersion.contains("SNAPSHOT"))
        {
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }

        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

apply plugin: 'java' 

project.version="20.7-SNAPSHOT" // this can use your own versioning scheme

// These two plugins bring in the standard tasks for building a LabKey module that may include JSPs, XSDs, or npm builds
apply plugin: 'org.labkey.base'
apply plugin: 'org.labkey.module'


repositories
        {
            // Use this repository when relying on release versions of the LabKey artifacts and their external dependencies
            maven {
                url "${project.artifactoryContextUrl}/libs-release"
            }
            // Use this repository when relying on snapshot versions of LabKey artifacts or requiring snapshot external dependencies
            maven {
                url "${project.artifactoryContextUrl}/libs-snapshot"
            }
            jcenter() // include the bintray/jcenter repository separately in case lookup with previous repository fails
        }

dependencies
        {
            implementation "org.labkey.api:issues:${labkeyVersion}" // An example of declaring a dependency on the API jar for a module
            external "commons-beanutils:commons-beanutils:${commonsBeanutilsVersion}" // An external dependency to be included in the module's lib directory
        }

